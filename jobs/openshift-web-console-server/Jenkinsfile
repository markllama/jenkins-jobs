#!/usr/bin/env groovy

@Library('artlib')
import com.redhat.art.*

// Expose properties for a parameterized build
properties(
    [
        buildDiscarder(
            logRotator(
                artifactDaysToKeepStr: '',
                artifactNumToKeepStr: '',
                daysToKeepStr: '',
                numToKeepStr: '1000')),
        [
            $class: 'ParametersDefinitionProperty',
            parameterDefinitions: [
                [
                    name: 'TARGET_NODE',
                    description: 'Jenkins agent node',
                    $class: 'hudson.model.StringParameterDefinition',
                    defaultValue: 'container'
                ],
                                [
                    name: 'GITHUB_OWNER',
                    description: 'Github owner for source repos',
                    $class: 'hudson.model.ChoiceParameterDefinition',
                    choices: [
                        "openshift",
                        "markllama"
                    ].join("\n"),
                    defaultValue: 'openshift'
                ],
                [
                    name: 'SSH_KEY_ID',
                    description: 'SSH credential id to use',
                    $class: 'hudson.model.ChoiceParameterDefinition',
                    choices: [
                        "openshift-bot",
                        "markllama-aos-cd-bot",
                        "markllama"
                    ].join("\n"),
                    defaultValue: 'markllama'
                ],
                [
                    name: 'BUILD_VERSION',
                    description: 'OCP Version to build',
                    $class: 'hudson.model.StringParameterDefinition',
                    defaultValue: 'master'
                ],
                [
                    name: 'RELEASE',
                    description: 'build local or release',
                    $class: 'hudson.model.BooleanParameterDefinition',
                    defaultValue: false
                ],
                [
                    name: 'SCRATCH',
                    description: 'build an unofficial release',
                    $class: 'hudson.model.BooleanParameterDefinition',
                    defaultValue: true
                ],
                [
                    name: 'DEBUG',
                    description: 'ask commands to print details',
                    $class: 'hudson.model.BooleanParameterDefinition',
                    defaultValue: false
                ],
                [
                    name: 'MOCK',
                    description: 'dont do anything if true',
                    $class: 'hudson.model.BooleanParameterDefinition',
                    defaultValue: false
                ],
            ]
        ],
        disableConcurrentBuilds()
    ]
)

// convert property strings to proper logical values
release = RELEASE.toBoolean()
scratch = SCRATCH.toBoolean()
mock = MOCK.toBoolean()
debug = DEBUG.toBoolean()
debug_shell = debug ? "-x" : "" 

// ***************************************************************************
//
//
//
// ***************************************************************************

// Initialize $PATH and $GOPATH
def gopath_setup() {
    echo "Adding git managed script directories to PATH"

    GOPATH = "${env.WORKSPACE}/go"
    env.GOPATH = GOPATH
    sh "rm -rf ${GOPATH}"  // Remove any cruft
    sh "mkdir -p ${GOPATH}"
    echo "Initialized env.GOPATH: ${env.GOPATH}"
}

def wrap_script(shell, collection=null) {
    if (collection) {
        echo "running build tools in collection ${collection}"
        wrapper = "scl enable ${collection}"
        return { script -> shell.sh("${wrapper} '${script}'") }
    }

    return { script -> shell.sh script }
}

def Boolean package_installed(String package_name) {
    return sh(returnStatus: true, script: "rpm -q --quiet ${package_name}") == 0
}

// ***************************************************************************
//
//
//
// ***************************************************************************

node(TARGET_NODE) {
    checkout scm
    AOS_CD_JOBS_COMMIT_SHA = sh(
        returnStdout: true,
        script: "git rev-parse HEAD",
    ).trim()
    
    package_name = "origin-web-console-server"
    upstream_name = "origin-web-console"
    branch_prefix = "enterprise-"
    brew_task_url_prefix = 'https://brewweb.engineering.redhat.com/brew/taskinfo?taskI='

    go_source_dir = "go/src/github.com/${GITHUB_OWNER}"

    // clean up before starting
    sh "rm -rf ${go_source_dir}"
    sh "mkdir -p ${go_source_dir}"
    sh "rm -rf BUILD"

    package_path = go_source_dir + "/" + package_name
    upstream_path = go_source_dir + "/" + upstream_name

    package_repo = new GitHubRepository(
        [
            owner: GITHUB_OWNER,
            project: package_name,
            path: package_path,
            pipeline: this
        ])

    upstream_repo = new GitHubRepository(
        [
            owner: GITHUB_OWNER,
            project: upstream_name,
            path: upstream_path,
            pipeline: this
        ])

    sshagent([SSH_KEY_ID]) {
        releases = package_repo.releases(branch_prefix)
    }

    if (BUILD_VERSION == 'master') {
        build_version = new Version(releases[-1])
        echo "detected version: ${build_version}"
    } else if (BUILD_VERSION == 'stage') {
        error("Not implemented: build should be most recent numbered branch")
    } else {
        build_version = new Version(BUILD_VERSION)
    }

    if (build_version > new Version("3.11")) {
        if (package_installed("go-toolset-7")) {   
            collection = 'go-toolset-7'
        } else {
            error("missing package go-toolset-7 is required for version $build_version")
        }
    } else {
        collection = null
    }
    def shell = wrap_script(this, collection)

    gopath_setup()
 
    // select the branch to build
    // NOTE: Valid build versions are only those in the releases.
    // master should not be used, but can default to the most recent release
    def branch_name

    if (releases.find { it == build_version } != -1) {
        branch_name = branch_prefix + build_version
        if (debug) {
            echo("branch_name: ${branch_name}")
        }
    } else {
        error "invalid build version ${build_version}"
    }

    if (debug) {
        echo "build_version: ${build_version}, branch_name: ${branch_name}"
    }
    
    sshagent([SSH_KEY_ID]) {

        stage("clone ${package_name}") {
            package_repo.clone()
        }

        stage("clone ${upstream_name}") {
            upstream_repo.clone()
        }

        stage("configure dummy merge") {
            package_repo.config("merge.ours.driver", "true")
            package_repo.add_attributes()
        }

        stage("build upstream") {
            dir(upstream_repo.path) {
                sh "git merge origin/master --no-commit --no-ff"
                sh "bash ${debug_shell} ./hack/install-deps.sh"
                sh "grunt build"
                sh "git add dist"
                sh """git commit -m "Merge master into ${branch_name}" --allow-empty"""
                sh "git reset --hard HEAD"
            }
        }
        
        stage("merge upstream into ${upstream_repo.project}") {
            dir(package_repo.path) {
                // the resulting commit is used in the downstream merge message
                VC_COMMIT =  sh(
                    script: [
                        "GIT_REF=${branch_name} bash ${debug_shell} hack/vendor-console.sh 2>/dev/null",
                        "grep 'Vendoring origin-web-console'",
                        "awk '{print \$4}'"
                    ].join(" | "),
                    returnStdout: true
                ).trim()

                echo "VC_COMMIT - Commit = '${VC_COMMIT}'"

                if (VC_COMMIT == "") {
                    sh(
                        "GIT_REF=${branch_name} bash ${debug_shell} hack/vendor-console.sh"
                    )
                }
                sh "git add pkg/assets/bindata.go"
                sh "git add pkg/assets/java/bindata.go"
                sh "git commit -m 'bump origin-web-console ${VC_COMMIT}' --allow-empty"
            }
        }

        stage("tag and push") {
            echo "tag and push"
            dir(package_repo.path) {
                shell(
                    [
                        "tito tag",
                        (debug ? '--debug' : ''),
                        '--accept-auto-changelog',
                        '--use-version', "${build_version}.1",
                        '--use-release', '1'
                    ].join(' ')
                )
            }
        }

        stage("build") {
            //tito_release(this, package_name, new_version, scratch)
            //tito_build(this, package_name)
            def output_root = env.WORKSPACE + '/BUILD'
            dir(package_repo.path) {
                shell(
                    [
                        "tito build",
                        (debug ? '--debug' : ''),
                        '--offline',
                        '--rpm',
                        '--output', output_root,
                    ].join(' ')
                )
            }
        }

    }

    archiveArtifacts allowEmptyArchive: true, artifacts: "BUILD/**"
}
